import org.apache.tools.ant.taskdefs.condition.Os
plugins {
    id 'java'
    id 'org.springframework.boot' version '2.3.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'

}

project.ext {
    npmHome = 'src/main/webapp/ng-frontend/'
}

group 'com.array'
version '1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

task npmInstall(type:Exec) {
    workingDir = "${npmHome}"
    if(Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'npm.cmd','install'
    } else {
        commandLine 'npm','install'
    }

}

task npmBuildProd(type:Exec, dependsOn:'npmInstall') {
    workingDir = "${npmHome}"
    if(Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'npm.cmd','run','buildProd'
    } else {
        commandLine 'npm','run','buildProd'
    }

}

task doCBR {
    dependsOn 'clean'
    dependsOn 'npmBuildProd'
    dependsOn 'build'
    dependsOn 'bootRun'
    tasks.findByName('npmBuildProd').mustRunAfter 'clean'
    tasks.findByName('build').mustRunAfter 'npmBuildProd'
    tasks.findByName('bootRun').mustRunAfter 'build'
}

dependencies {
    implementation 'org.liquibase:liquibase-core:4.5.0'
    implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.4'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    runtimeOnly 'mysql:mysql-connector-java'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

